/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * This file incorporates changes by @continuumsecurity
 */

plugins {
    id 'java'
}

group 'com.continuumsecurity.elasticagent'
version '2.3.0'

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'com.continuumsecurity.elasticagent.ec2',
        version    : project.version,
        goCdVersion: '19.8.0',
        name       : 'EC2 Elastic Agent Plugin',
        description: 'AWS EC2 Elastic Agent Plugin for GoCD',
        vendorName : 'Continuum Security',
        vendorUrl  : 'https://github.com/continuumsecurity/GoCD-EC2-Elastic-Agent-Plugin'
]

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            resources.srcDirs += ['src/main/resource-templates']
        }
    }
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:23.5.0',
    ]
}

dependencies {
    compileOnly project.deps.gocdPluginApi

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation(group: 'com.google.guava', name: 'guava', version: '33.0.0-jre') {
        // see https://github.com/google/guava/pull/6606
        exclude(module: 'listenablefuture')
        exclude(module: 'error_prone_annotations')
        exclude(module: 'checker-qual')
        exclude(module: 'jsr305')
    }
    implementation group: 'joda-time', name: 'joda-time', version: '2.12.7'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.32'
    implementation group: 'software.amazon.awssdk', name: 'ec2', version: '2.25.38'

    testImplementation project.deps.gocdPluginApi
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.11.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.11.0'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true

        exceptionFormat 'full'

        beforeSuite { suite ->
            if (suite.parent) {
                logger.quiet("Running ${suite.name}")
            }
        }

        afterSuite { suite, result ->
            if (suite.parent) {
                logger.quiet("Tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
                if (result.resultType == TestResult.ResultType.FAILURE) {
                    logger.quiet("Test ${suite.name} FAILED")
                }
            } else {
                logger.quiet("Total tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
            }
        }
    }
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }

    from(sourceSets.main.java) {
        into "/"
    }
}