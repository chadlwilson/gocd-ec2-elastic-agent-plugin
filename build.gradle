/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * This file incorporates changes by @continuumsecurity
 */

plugins {
    id 'java'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

group 'com.continuumsecurity.elasticagent'
version '2.3.0'

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'com.continuumsecurity.elasticagent.ec2',
        version    : project.version,
        goCdVersion: '19.8.0',
        name       : 'EC2 Elastic Agent Plugin',
        description: 'AWS EC2 Elastic Agent Plugin for GoCD',
        vendorName : 'Continuum Security',
        vendorUrl  : 'https://github.com/continuumsecurity/GoCD-EC2-Elastic-Agent-Plugin'
]

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            compileClasspath += configurations.compileOnly
            runtimeClasspath += configurations.compileOnly
            resources.srcDirs += ['src/main/resource-templates']
        }
    }
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '19.8.0'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.10.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.29'
    compile group: 'software.amazon.awssdk', name: 'ec2', version: '2.10.30'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.0.0'
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.0.0'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true

        exceptionFormat 'full'

        beforeSuite { suite ->
            if (suite.parent) {
                logger.quiet("Running ${suite.name}")
            }
        }

        afterSuite { suite, result ->
            if (suite.parent) {
                logger.quiet("Tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
                if (result.resultType == TestResult.ResultType.FAILURE) {
                    logger.quiet("Test ${suite.name} FAILED")
                }
            } else {
                logger.quiet("Total tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
            }
        }
    }
}

jar {
    from(configurations.compile) {
        into "lib/"
    }

    from(sourceSets.main.java) {
        into "/"
    }
}